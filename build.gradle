apply plugin: 'java'

project.ext {
    javasimonVersion = '3.3.0'
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.1',
            'org.slf4j:slf4j-simple:1.7.1',
            'commons-configuration:commons-configuration:1.6',
            'org.apache.commons:commons-lang3:3.1'
    testCompile 'junit:junit:4.10',
            'com.google.code.gson:gson:2.2.2',
            'org.javasimon:javasimon-core:' + project.ext.javasimonVersion
}

tasks.withType(Test) {
    testLogging.showStandardStreams = true
    
    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle(":" + descriptor)
    }
}

task itest(type: Test) {
    include "**/*ITest*"
}
itest.dependsOn(cleanItest)

//http://www.gradle.org/docs/current/dsl/org.gradle.api.tasks.testing.Test.html
test {    
    exclude "**/*ITest*"
    minHeapSize = "128m"
    maxHeapSize = "512m"
    jvmArgs '-XX:MaxPermSize=256m'
}

task listCache << {
    configurations.compile.each { println it } 
}

task listJars << {
    configurations.compile.each { File file -> println file.name }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}
